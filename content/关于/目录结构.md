---
title: "目录结构"
date: 2015-04-10 16:31
---
[TOC]


介绍微度日历的目录结构

client(客户端代码)
========


css(全局样式)
-------

###fontAwesome(icon-font的辅助样式)


###globals(全局的样式)

```
- base.import.less              清除默认样式
- list-items.import.less                    
- button.import.less            按钮的样式
- menu.import.less
- form.import.less        
- message.import.less
- header.import.less            header头的样式
- nav.import.less
- layout.import.less      
- notification.import.less      通知的样式
- link.import.less        
- sidebar.import.less 
```

###util(样式辅助工具)

```
- helpers.import.less           一些全局的辅助样式，值得注意的为.position
- reset.import.less     
- typography.import.less
- lesshat.import.less                       自动加前缀的工具，遇到兼容性问题就进来查，比较重要的文件
- text.import.less    
- variables.import.less         所有的变量，尽量不要在零散的css里面写颜色和数值，后期不好修改，请把所有公共的变量写入这个文件，十分重要！！！！
```

###main.less(引用所需的less文件)   

如果有新的less文件记得引入进来，否则是不起作用的，用法为`@import 'globals/base.import.less'`

lib(前台公用代码)
-------

###common.coffee(前台公共函数)

声明了`Common.client`命名空间,前台的公共函数全部存储到其中

`Common.client.static`为静态资源的命名空间

####时间操作

#####switchDate(type: String, back: Boolean)

```
参数: type值为['days', 'weeks', 'months'],back为true时,为后退
功能:切换日期的时间函数,例如在切换上一月下一月,前一天后一天
原理:通过moment.js做日期的增减操作,然后操作Session
```

#####setCuTime(data)

```
参数:data = {
    year,
    month,
    date
}
功能:设置当前时间
原理:通过控制Session切换当前时间
```

#####goToday()
`跳转到当前系统时间`

#####createMonth()
`生成当月的数据,主要使用在月视图`

#####filterByHour(collection, date)

```
参数: collection为数组,从数据库中查询后fetch的数据
     date = {
     year: Number
     month: Number
     date: Number
     hour: Number
}
功能: 根据起始时间筛选数据,主要使用在计算待办事项
```

####页面效果

#####showModal(templateName, data, dom)   ---  重要!

```
参数: templateName弹出层的模板名, data为渲染弹出层的数据, dom为插入的地方,默认是body
功能: 删除前一个弹出层,并新渲染一个弹出层,避免同时存在的弹出层太多,不好管理的问题,请使用新弹出层的时候以这个方法来插入
```

####数据相关

#####allShareMember()

`在弹出层中计算出当前能分享的人和群组,用于新建日程和修改日程`

####订阅操作

#####subfriendInfo()

`再次订阅friendInfo数据,原因是因为Meteor.users不是活性数据`

#####refreshSub(name)

`刷新订阅数据`

#####subNotificationName(arr)

`根据消息id来订阅回消息的描述`

###registerHelper.coffee(全局的helper方法)

####{{readAvatar id}}

```
参数: 用户id
功能: 返回用户的头像
原理: 查用户的icon字段,如果是二进制的就返回,如果不是就去线上读
```

####{{getNotifyName obj}}

```
参数: obj = {
    operate_id: String
    target_id: String
    event_type: String
}
功能: 根据消息的信息组合出消息的内容
```

###setting.coffee(前台第三方的插件设置)

第三方弹出框的设置
```
effect:    动画类型
position:  出现的位置
timeout:   出现的时间
```

view(视图代码)
-------

###auth(登陆注册)

####auth-join.coffee

> 注册的js文件，后期需要调整的也就是`Template.join.events`里面的两个事件了

> **这里需要注意的是，如果登陆有错误信息，请写入`Session.set(ERRORS_KEY, value)`**

> **注册发验证码仍为未完成状态，请完善**

####auth-join.html

> 注册的页面文件，没什么需要改动的

####auth-signin.html

> 登陆的页面，没什么好需要改动的地方

####auth-signin.js

> 登陆的js文件

####auth.import.less

> 登陆注册的样式文件

###calendar(日历主文件)

####calendar-detail.coffee

> 日历版块的一些小事件，不太重要，主要功能有右侧迷你日历的一些事件，还有中间顶部选项卡的事件

> 需要注意的是`Common.client.switchDate`函数,第一个参数是需要变换的类型,第二个是增还是减

####calendar-detail.html

> 日历部分主视图，主要是框架模板

####calendar-item.html

> 包含：

> 1. 今天的模板

> 2. 日视图模板

> 3. 周视图模板

> 4. 月视图模板

> 5. 每一条日程的模板

####calendar-item.import.css

> 以上5个模板的样式

####calendar.import.less

> 主要是calendar-detail.html的样式

####date-item.coffee

> 日程的逻辑，需要注意的是rendered和onDestroyed方法，

> rendered里面是新建日个日程然后计算该时段有的日程数，做width和left的重新赋值

> onDestroyed方法是删除一个日程的时候，重新计算该时段width和left的方法

> 具体细节请看注释

####day-view.cofeee

> 日视图的操作逻辑，需要注意的是`helpers`里面有个`hours`，这个的逻辑是,先找出一个人今天的任务(DB.Calendar.findByDate方法),然后根据小时去筛选(Common.client.filterByHour方法)

> 点击一个时段的方法中,`Common.client.showModal`是显示模态框的方法

####month-view.coffee

> 月视图的操作逻辑,需要注意的是`Common.client.createMonth()`方法,这个和小日历里面的显示逻辑是公用的,具体详情请看方法代码

> 'click .month-cal td'这个方法需要注意,这里是点击某一天,因为在共享模板里面是复用月视图模板的,所以这里针对路由做了判断处理

####today-view.coffee

> 今天视图的模板,需要注意的只有,`DB.Calendar.findNearlyDate`函数,第一个参数是类型,第二个是筛选出来的最大条数,默认5条

####week-view.coffee

> 周视图模板,大多数代码和日视图相同,逻辑复杂的地方主要是有`helper`中的`hours`,第一层循环是时间,但是因为每天的数据是一样的,所以我就把每天的时段存储在一个缓存变量中,这样就第一次处理的时候需要从数据库读取数据筛选,之后的时段就不需要了,这里有个coffee的用法就是

```javascript
arr = for i in [1,2,3,4]
    {
    a: i
}
```

> 这种写法的类似

```javascript
var arr = []
for(var i = 0; i < [1,2,3,4].length; i++) {
    arr.push({a: [1,2,3,4][i]});
}
```

###group(好友群组)

####group.coffee

> 群组页面的操作逻辑,需要注意的是,`Template.group.helpers`里面的`members`,这个地方用`transform`做了一下处理,筛选出了没同意的,和已经被拒绝的,注意,使用`transform`方法请在后面记得加上`.fetch()`

> 第二个值得注意的方法是`Template.friendInfo.helpers`中的`commonCal`,这个是计算和目标人员的共同任务用的

####group.html

> 群组模板

####group.import.less

> 群组模板的样式

###header(头部)

####header.coffee

> 头部状态了的逻辑,需要注意的是,这里包含了`Notification`的逻辑,`DB.Notification.eventType[eventType]`中,根据`dom`上的`data-event`字段来判断使用哪个方法,`DB.Notification`定义在`lib/collection/DB-notification.coffee`中

####header.html

> 头部状态栏的dom结构

###modal(模态框)

####add-group.coffee

> 添加群组的逻辑,这里需要注意的有:

> 1. `Schemas.addGroup`是创建了一个数据模型,调用了第三方的`autoform`插件,及其好用,具体请前往`atmosphere`上查看具体情况

> 2. 添加群组是调用`DB.Groups.addGroup`方法

> 3. 修改群组信息是`DB.Groups.updateMemberList`方法,退出群组就是变相的修改群组

####add-group.html

> 群组相关的模态框模板

####add-person.coffee

> 添加联系人的操作逻辑,需要注意:

> 1. `Meteor.call('findPerson')`方法,因为数据是在后台,没有抛到前台来,所以得在后台定义方法,前台调用

> 2. `click #modalConfirm`方法,如果是邀请人员没有补充

> 3. `DB.Friend.sendQueryAddPerson`申请添加好友的函数

####add-person.html

> 添加联系人的模态框模板

####modal.coffee

> 包含三个模板的操作逻辑: 添加日程,修改日程,个人设置

> 需要注意

> 1. 定义了一个schema,包含了一个日程的数据模型,使用auto-form自动生成表单

> 2. 添加日程的时候过程麻烦,逻辑不麻烦,修改也是一样

> 3. 注意rendered方法,主要是用来获取点击的时候的数据的,因为我的模态框每次点击都是先删除再添加,所以每次都会触发rendered事件

> 4. 个人设置中,上传头像用的方法是解析成二进制文件,然后存储在数据库中

####modal.html

> 包含三个模块:添加日程,修改日程,个人设置

####modal.import.less

> 所有模态框的样式

###share(分享)

####share.coffee

> 分享部分的模板,这里的members算的时候,是只算出了分享给你日历的人

####share.html

> 分享部分的模板

####share.import.less

>分享部分的所有样式

###sidebar(侧栏)

####sidebar.coffee

> 就一个选项卡

####sidebar.html

> 侧栏的模板

###toolTemplate(loading和404)

这里面都是todolist留下来的

###app-body.html(layout模板)

yield模板

###app-body.js(没啥用)

head.html(头文件)
--------


lib(前后台公用代码)
=======

collection(表)
-------

注意: 尽量把所有表的操作,都离散成小的功能,然后在表的实例上添加该方法,不要在逻辑区域添加修改数据库的函数,这样不方便以后更改

###DB-calendar.coffee   

> 定义了DB.Calendar表,并为这个表的实例添加了一些方法

####查询方法

findByDate(userId: String, date: Object)

```
参数: userId是目标用户的id,date = {
    year: Number
    month: Number
    date: Number
}
功能: 算出某人当天的所有日程
```

findNearlyToDo(limitNum: Number)

```
参数: limitNum是限制的数目
功能: 找最近代办的日程
```

findNearlyCom(limitNum: Number)

```
找到最近已经完成的日程(注意,这里没通过时间去找,而是找到所有完成的任务然后通过排序取了最新的几条)
```

findShareDate(limitNum: Number)

```
找到最近分享给别人的日程
```

findBeShareDate(limitNum: Number)

```
找到最近别人分享给你的日程
```

findNearlyDate(type: String, limitNum: Number)

```
参数: 第一个参数是判断是哪种行为,然后调用上面4种方法的其中一种
注意,其他类似这种逻辑的函数请也这样设计,尽量少使用if...else if... else语法
```

####修改方法

> 参数带问好说明可选,所有cb代表回调函数

addDate(json: Object, cb?: Function)

```
参数: json ={
    create_id: String
    include: Match.Optional Array
    start_time: Number
    end_time: Number
    content: String
    title: String
    groups: Array
}
功能: 添加一个日程
```

updatePersonDate(json: Object, cb?: Function)

```
参数同上
功能: 修改一个日程
```

delDate(json: Object, cb?: Function)

```
为修改日程服务,逻辑如下：如果是创建者删除该日程，即更改日程的status,大家都看不见

如果不是创建删除，即更改include内相应地数据，即不关注
```

####表权限

insert: 创建人id和当前用户相同的情况下可以创建

update:

> 1. 自己能改自己的

> 2. 我能改别人没锁的


###DB-friend.coffee   

创建`DB.Friend`实例,并扩展方法

####表权限

insert: _id和操作人id相同

update: _id和操作人id相同

####添加好友的方法

friendMyInfo

```
取出自己的好友信息
```

initFriend(id: String, cb: Function, isAgree: Boolean)

```
参数: id需要添加好友的id, cb:毁掉函数, isAgree: 是否初始化的时候就同意加为好友
功能: 当自己没有friend文档的时候,建立一个自己的friend文档,修改完后重新订阅friendInfo,因为用户表不是活性数据
```

updateFriends(friends, cb)

```
修改自己的friend表列,修改完后重新订阅friendInfo
```

sendQueryAddPerson(id, cb)

```
发送好友请求,存在多种情况,逻辑相当复杂..
一般只需要使用即可
```

allowQueryAddPerson(id, cb)

```
同意好友邀请
操作逻辑
1，将申请人中对应的status改为1，当对方已经是1的时候报错，如果不是，改为1
2，将自己的friend表列中加上申请人，如果已经存在就更改status的值，逻辑同上 
```

denyQueryAddPerson(id, cb)

```
逻辑和上面两个方法基本一致
```

###DB-group.coffee

创建实例`DB.Groups`(没用group是因为group在MongoDB里面是关键字)

####表权限

insert: 创建人id和操作人相同

update: 同上

remove: 同上

####查询方法

findInGroup(id)

```
查找某个人和你有交集的群组
```

getMemberListById(id)

```
根据Id返回memberList
```

####操作方法

addGroup(name: String, member_list: Array, cb: Function)

```
新建一个群组
```

updateMemberList(id: String, json: Object, cb: Function)

```
修改一个群组里面的member_list字段
```

removeGroup(id: String, cb: Function)

```
删除群组
```

###DB-init.coffee  

在后台实例化了`DB.LoginInfo`和`DB.VCode`  

DB.VCode.getCode(phone: String)

```
获取该手机号的验证码
```  

###DB-notification.coffee   

> 实例化了`notification`表

####表权限

insert: 操作人id或目标人id为自己

update: 同上

####notification的设计标准

> DB.Notification.msg命名空间存放的是每种类型消息的描述文字

> DB.Notification.eventType命名空间存放的是每种消息的处理办法,无需理解,会调用即可,请保持这样的设计模式不变

####notification的相关方法

insertNotification(id: String, fnName: String, data?: anyType, cb: Funtion)

```
参数:fnName指的是生成这条消息的时候的函数名,也就是eventType,data是可选值,存放处理这条消息的时候需要用到的数据
功能: 生成一条消息 
```


###DB-user.coffee

> 对`Meteor.users`做扩展

####表权限

update: 仅允许修改emails和profile字段

####辅助函数

getUserNameById(id)

```
根据id返回名字
```

getShareCal()

```
获取分享给我日历的人的id
```

####人员操作

invitePerson(keyword: String, cb: Function)

```
参数: keyword用于查找人员,phone或者email
```

####共享操作

shareCalToMem(id: String, type: String, cb: Function)

```
调用shareMyCal方法和cancelShareMyCal来完成相应功能，相当于入口函数
```

shareMyCal(id: String, cb: Function)

```
共享自己的日历给某人
```

cancelShareMyCal(id: String, cb: Function)

```
取消日历的共享
```

router(路由)
-------

###initRouter.coffee

这个文件主要是路由的一些设置

```javascript
Router.onBeforeAction( ->
    if !Meteor.userId()
        Router.go 'signin'
    else
        this.next()
, {
    except: ['join', 'signin']
})
```

这是判断除了登陆和注册以外的所有界面,如果没登陆,就跳登陆

```javascript
Router.onBeforeAction ->
    if Meteor.userId()?
        Router.go 'home'
    else
        this.next()
, {
    only: ['join', 'signin']
}
```

这是判断登陆和注册界面,如果已经登陆了,就不允许进入这两个界面

```javascript
# 等订阅完
Router.onBeforeAction ->
    this.next() if this.ready() 

# 全局订阅用户信息
Router.waitOn ->
    Meteor.subscribe 'userData'
    Meteor.subscribe 'calendar'
    Meteor.subscribe 'friend'
    Meteor.subscribe 'friendInfo'
    Meteor.subscribe 'notification'
    Meteor.subscribe 'group'
, {
    except: ['join', 'signin']
}
```

这一段是订阅相关的

###router.coffee

这是主要的路由文件

```
Router.configure 
    layoutTemplate: 'appBody'
    notFoundTemplate: 'appNotFound'
    loadingTemplate: 'appLoading'
```

设置布局模板,404模板,loading模板

```
# 记录当前时间的session变量名
CULOOK = 'cuLook'
DEFAULT_VIEW = 'today'
VIEW_TYPE = 'viewType'
ERRORS_KEY = 'joinErrors'

# 当前查看的人员
CU_MEMBER = 'cuMember'
```

把Session的名字都提前声明,这样如果做出修改的时候,直接改这个变量就行了

```
Router.map ->
```

这里开始定义路由规则

```
# 主页
this.route 'home', 
    path: '/',
    action: ->
        Router.go('calendar') if this.ready()


#  注册
this.route 'join', 
    path: '/join',
    layoutTemplate: '',
    action: ->
        if Template.join.timer
            Meteor.clearInterval Template.join.timer
        this.render()

# 登陆
this.route 'signin', 
    path: '/signin',
    layoutTemplate: '',
    action: ->
        this.render()
```

这里是主页,注册,登陆的路由设置,其中注册的clearInterval是除去发验证码的定时器用的

```
#  日历页面
this.route 'calendar', 
    action: ->
        now = Date.now()
        # 获取当前查看的视图模式
        view = this.params.query.view or DEFAULT_VIEW
        Common.client.goToday() if view is 'today'
        # 设置默认Session
        Session.setDefault CULOOK, now
        Session.set CU_MEMBER, null
        # 设置视图模式
        Session.set VIEW_TYPE, view
        this.render()
```

日历的路由逻辑,从路由地址中读取当前的视图类型,然后操作Session

```
# 群组页面
this.route 'group', 
    path: '/group/:_id?'
    action: ->
        Session.set CU_MEMBER, this.params._id
        this.render()


# 共享页面
this.route 'share', 
    path: '/share/:_id?'
    action: ->
        view = this.params.query.view or 'month'
        Session.set VIEW_TYPE, view
        Session.set CU_MEMBER, this.params._id
        this.render()
```

群组和共享的页面,也是通过路由地址来控制Session


base.coffee(初始化的命名空间定义)
----

用来初始化命名空间,做了momentJs的国际化处理

public(静态资源)
======

fonts(字体文件)
------

img(图片资源)
------


server(后台代码)
======

###accounts-helper.coffee(登陆的辅助方法)

登陆需要用到的辅助方法

```
# 注册时加上md5、注册时间
Accounts.onCreateUser (options, user) ->
    profile = options.profile
    if profile
        password = profile.passwordCache
        encryption = crypto.createHash 'md5'
                        .update password, 'utf8'
                        .digest 'hex'
        regtime = Date.now()
        profile.encryption = encryption if encryption
        profile.regtime = regtime
        delete profile.passwordCache
        user.profile = profile
    return user;

# 登陆的时候更新登陆日志
Accounts.onLogin (info) ->
    userId = info.user._id
    oDate = moment()
    loginInfo = {}
    loginInfo.member_id
    loginInfo.logindata = oDate.format 'YYYY-MM-DD'
    loginInfo.logintime = oDate.format 'YYYY-MM-DD HH:MM:SS'
    loginInfo.loginTimeInt = oDate.format 'x'
    loginInfo.type = 'w'
    DB.LoginInfo.add loginInfo
```

这两个方法是用于处理注册和登陆的,前者是在注册的时候添加md5加密的密码,还有一些相关信息,后者是登陆的时候在login表里面记录登陆的信息

```
# 短信格式和内容
getMsgContent = {}
getMsgContent.setCode = (phone) ->
    return "#{}"
```

设置短信的内容,未完成,加上短信接口的时候只需要补全这个函数,然后去调用即可

```
# 设置验证码
Meteor.methods {
    setCode: (phone) ->
        old = DB.VCode.getCode phone
        code = _.random 100000, 999999
        # 先判断这个手机号是否已经注册了
        man = Meteor.users.findOne {
            username: phone
        }
        if man
            return {
                code: 0
                result: '改手机号已注册'
            }
        now = Date.now()
        console.log "#{phone}的验证码为#{code}"
        msg = getMsgContent.setCode phone
        if old.code
            VCodeId = old.result._id
            isSuccess = DB.VCode.update {
                _id: VCodeId
            }, {
                $set: 
                    code: code
                    create_time: now
            }
            if isSuccess
                # 发短信给用户
                Meteor.call 'sendMsg', phone, msg
                return {
                    code: 1
                    result: 
                        code: code
                        createTime: now
                }
            else
                return {
                    code: 0
                    result: '获取验证码错误，请稍后重试'
                }
        else 
            isSuccess = DB.VCode.insert {
                phone: phone
                code: code
                create_time: now
            }
            if isSuccess
                # 发短信给用户
                Meteor.call 'sendMsg', phone, msg
                return {
                    code: 1
                    result: 
                        code: code
                        createTime: now
                }
            else
                return {
                    code: 0
                    result: '获取验证码错误，请稍后重试'
                }

    # 发送短信的方法
    # 未完成
    # 未完成
    # 未完成
    sendMsg: (phone, msg) ->
        check phone, String
        check msg, String
    # 未完成
    # 未完成
    # 未完成
    # 未完成
    # 未完成
}
```

这是发送验证码的两个方法,第一个是生成验证码,第二个是发送验证码,未完成

###member.coffee(成员的后台方法)

人员相关的后台方法,因为有些数据没推送到前台,所以通过后台计算后返回前台

```
findPerson: (keyword) ->
    check keyword, String
    # 搜索手机号就过滤掉email
    man = Meteor.users.findOne {
        username: keyword
    }, {
        fields:
            services: false
            emails: false
    }
    unless man
        # 过滤掉手机号
        man = Meteor.users.findOne {
            'emails.0.address': keyword
        }, {
            fields:
                services: false
                username: false
        }
    if man  
        return {
            result: man
        }
    else 
        return {
            error: '无搜索结果'
        }
```

用于添加联系人的时候,搜索人员使用,输入邮箱或手机查找相关人员,精确搜索

```
# 返回固定人员的名字
getPersonName: (id) ->
    check id, String
    somebody = Meteor.users.findOne id
    if somebody
        return somebody.profile.name
    else
        return '未知用户'
```

根据id返回对应人员的名字使用

```
# 同意好友请求之后修改申请人的status状态
# id是申请人的id
agreeFriendQuery: (id, status) ->
    check id, String
    check status, Number
    myId = this.userId
    console.log myId, status
    return DB.Friend.update {
        _id: id
        'friends._id': myId
    }, {
        $set: 
            'friends.$.status': status
    }
```

这个是同意别人的好友请求的时候调用的方法,因为同意的那一瞬间,还不是好友,所以联系人的资料没有提送过来,需要通过后台修改

###publish.coffee(发布逻辑)

发布数据的逻辑

```
Meteor.publish 'userData', ->
    userId = this.userId
    if userId?
        return Meteor.users.find {
            _id: userId
        }, {
            fields: {
                services: false
            }
        }
    else
        this.ready()
        return
```

发布用户数据的逻辑,除了services字段,其余抛出去,这里只订阅自己的数据

```
Meteor.publish 'calendar', (idArr, filter) ->
    userId = this.userId
    idArr = idArr or [userId]
    query = {
        $or: [{
            create_id: 
                $in: idArr
        }, {
            include:
                $elemMatch: 
                    _id: 
                        $in: idArr
                    status: 0
        }]
    }
    filter = filter or {}
    return DB.Calendar.find query, filter
```

日历的发布逻辑,发布所有自己创建的和自己参与的日历

```
# 发布我的联系人
Meteor.publish 'friendInfo', ->
    userId = this.userId
    # 找到我自己
    me = DB.Friend.findOne {
        _id: userId
    }
    # 取到friends
    friends = me and me.friends or []
    if friends.length > 0
        friendId = _.pluck friends, '_id'
        return Meteor.users.find {
            _id:
                $in: friendId
        }, {
            fields:
                services: false
        }
    else
        this.ready()
```

根据我的friend数据,去users表里订阅相关数据,**不同于订阅friend**,这个是订阅联系人在users表里的数据,不是订阅自己在DB.Friend里的数据

```
Meteor.publish 'friend', ->
    userId = this.userId
    return DB.Friend.find {
        _id: userId
    }
```

订阅我在friend表里面的数据

```
Meteor.publish 'notification', ->
    userId = this.userId
    return DB.Notification.find {
        $or: [{
            target_id: userId
        }, {
            bulletin: true 
        }]
    }
```

发布通知,发布目标者是自己的通知,或者公告(bulletin: true)

```
Meteor.publish 'notificationName', (arr) ->
    check arr, Array
    if arr.length > 0
        return Meteor.users.find {
            _id: 
                $in: arr
        }, {
            fields:
                _id: true
                'profile.name': true
        }
    else 
        this.ready()
```

发布通知里面涉及到的人员的名字,有时候你的通知内容涉及到陌生人,是取不到他们名字的,需要通过这个来发布出去

```
Meteor.publish 'group', ->
    userId = this.userId
    return DB.Groups.find 
        member_list: 
            $in: [userId]
```

发布群组数据,自己所在的群组

**这里需要注意,如果要更改订阅,尽量不要在原有基础上做更改,而是多配置一个发布规则去订阅**